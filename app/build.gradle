apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: "androidx.navigation.safeargs.kotlin"

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"
    defaultConfig {
        applicationId "hristostefanov.minibankingdemo"
        // set to 26(O) for the sake of Java 8 java.time package, to support older devices
        // Joda Time library can be used
        minSdkVersion 26
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            // this app does not support the production environment
//            buildConfigField("String", "SERVICE_BASE_URL", '"https://api.starlingbank.com"')
        }

        debug {
            buildConfigField("String", "SERVICE_BASE_URL", '"http:mock"')
        }

        sandbox {
            initWith debug
            buildConfigField("String", "SERVICE_BASE_URL", '"https://api-sandbox.starlingbank.com"')
        }
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    dataBinding {
        enabled = true
    }
}

dependencies {

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.core:core-ktx:1.3.1'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    // explicitly referenced for keeping up with security updates and
    // for use of toMediaType() and toResponseBody() extension functions
    implementation 'com.squareup.okhttp3:okhttp:4.2.2'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.2.2'

    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:retrofit-mock:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.2'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.2`'

    // for lifecycleScope extension property
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.2.0'
    // for viewModelScope extension property
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0'
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:2.3.0-alpha06"

    implementation 'com.google.dagger:dagger:2.28'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    kapt 'com.google.dagger:dagger-compiler:2.28'

    def nav_version = "2.3.0"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    def eventbus_version = '3.2.0'
    implementation "org.greenrobot:eventbus:$eventbus_version"
    kapt "org.greenrobot:eventbus-annotation-processor:$eventbus_version"

    // for EncryptedSharedPreferences
    implementation 'androidx.security:security-crypto:1.1.0-alpha02'

    ////////////////////////////////////////////////////////////////////////////////////////////////

    testImplementation 'junit:junit:4.13'

    // hamcrest-1.3 is a transitive dependency of junit 4, and cannot be upgraded to hamcrest 2.2
    // hamcrest library is needed for org.hamcrest.Matchers.comparesEqualTo
    //noinspection GradleDependency
    testImplementation 'org.hamcrest:hamcrest-core:1.3'
    //noinspection GradleDependency
    testImplementation 'org.hamcrest:hamcrest-library:1.3'

    testImplementation "org.mockito:mockito-core:3.3.3"
    // for mocking final classes (on JVM!)
    testImplementation 'org.mockito:mockito-inline:3.3.3'

    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.4.2'

    testImplementation "androidx.arch.core:core-testing:2.1.0"

    ////////////////////////////////////////////////////////////////////////////////////////////////

    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    debugImplementation 'androidx.fragment:fragment-testing:1.2.5'
    androidTestImplementation "org.mockito:mockito-core:3.3.3"

    // Using the "inline" edition of "dexmaker" by LinkedIn, allows mocking final classes
    // on Android P (9) or later. However v.2.25.0 and 2.25.1 do not work with
    // com.android.tools.build:gradle: 3.6.x (but worked with 3.5.x)
    // For this reason we use dexmaker-mockito instead of dexmaker-mockito-inline
    // and make the classes and members being mocked `open` until the issue is resolved.
//    androidTestImplementation 'com.linkedin.dexmaker:dexmaker-mockito-inline:2.25.1'
    androidTestImplementation 'com.linkedin.dexmaker:dexmaker-mockito:2.25.1'

    androidTestImplementation "androidx.navigation:navigation-testing:$nav_version"
}

kapt {
    arguments {
        arg('eventBusIndex', 'hristostefanov.minibankingdemo.util.EventBusIndex')
    }
}
