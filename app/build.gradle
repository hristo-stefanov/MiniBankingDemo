apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: "androidx.navigation.safeargs.kotlin"
apply plugin: 'dagger.hilt.android.plugin'

android {
    compileSdkVersion 31
    defaultConfig {
        applicationId "hristostefanov.minibankingdemo"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            // this app does not support the production environment for the time being
            buildConfigField("String", "SERVICE_BASE_URL", '"N/A"')
        }

        debug {
            buildConfigField("String", "SERVICE_BASE_URL", '"http:mock"')

            // TODO what to do with these
            buildConfigField("String", "CLIENT_ID", '"zBJLBbysdvjhkCedWxz0"')
            buildConfigField("String", "CLIENT_SECRET", '"m1U0y7vbV28k1LVbGvGy54Erk1SmY3DlU8kDM3SS"')
        }

        sandbox {
            initWith debug
            buildConfigField("String", "SERVICE_BASE_URL", '"https://api-sandbox.starlingbank.com"')
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    testOptions {
        unitTests {
            // needed when running local unit tests with Robolectric
            includeAndroidResources = true
        }
    }

    buildFeatures {
        viewBinding true
        dataBinding true
    }
}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'

    // explicitly referenced to use the latest version
    implementation 'com.google.android.material:material:1.4.0'

    // explicitly referenced for keeping up with security updates and
    // for use of toMediaType() and toResponseBody() extension functions
    implementation 'com.squareup.okhttp3:okhttp:4.9.1'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.2.2'

    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:retrofit-mock:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'


    def lifecycle_version = '2.4.0-rc01'
    // for lifecycleScope extension property, repeatOnLifecycle(), flowWithLifecycle
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    // for viewModelScope extension property
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:$lifecycle_version"
    // for LiveData.asFlow extension function
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"

    implementation "com.google.dagger:hilt-android:2.38.1"
    kapt "com.google.dagger:hilt-compiler:2.38.1"

    // 2.4.0-alpha10 allows creating safe args from SaveStateHandle
    def nav_version = "2.4.0-alpha10"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    def eventbus_version = '3.2.0'
    implementation "org.greenrobot:eventbus:$eventbus_version"
    kapt "org.greenrobot:eventbus-annotation-processor:$eventbus_version"

    // for EncryptedSharedPreferences
    implementation 'androidx.security:security-crypto:1.1.0-alpha03'

    ////////////////////////////////////////////////////////////////////////////////////////////////

    testImplementation 'junit:junit:4.13.2'

    // hamcrest-1.3 is a transitive dependency of junit 4, and cannot be upgraded to hamcrest 2.2
    // hamcrest library is needed for org.hamcrest.Matchers.comparesEqualTo
    //noinspection GradleDependency
    testImplementation 'org.hamcrest:hamcrest-core:1.3'
    //noinspection GradleDependency
    testImplementation 'org.hamcrest:hamcrest-library:1.3'

    testImplementation "org.assertj:assertj-core:3.20.2"

    testImplementation "org.mockito:mockito-core:3.8.0"
    // for mocking final classes (on JVM!)
    testImplementation 'org.mockito:mockito-inline:3.8.0'

    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.5.1'

    testImplementation "androidx.arch.core:core-testing:2.1.0"

    // for org.robolectric.annotation.Config annotation
    testImplementation 'org.robolectric:robolectric:4.5.1'

    // to run AndroidJUnit4 annotated local tests with Robolectric environment
    testImplementation 'androidx.test:core:1.4.0'

    testImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    testImplementation 'androidx.test.ext:junit:1.1.3'
    testImplementation 'androidx.test:rules:1.4.0'
    testImplementation "androidx.navigation:navigation-testing:$nav_version"

    // Hilt must be explicitly included for tests
    testImplementation 'com.google.dagger:hilt-android-testing:2.38.1'
    kaptTest 'com.google.dagger:hilt-android-compiler:2.38.1'

    def cucumber_version = '6.10.4'
    testImplementation "io.cucumber:cucumber-java:$cucumber_version"
    testImplementation "io.cucumber:cucumber-junit:$cucumber_version"

    // NOTE: "the library code goes into your application's APK instead of test APK
    // when you build your application in debug build type.
    // (Debug build type is used for instrumentation test by default)"
    // https://issuetracker.google.com/issues/122321150?pli=1
    //
    // "debugImplementation is used here so that the empty activity that FragmentScenario relies on
    // is accessible by the test target process"
    // https://developer.android.com/guide/fragments/test#configure
    debugImplementation 'androidx.fragment:fragment-testing:1.3.6'
}

kapt {
    arguments {
        arg('eventBusIndex', 'hristostefanov.minibankingdemo.util.EventBusIndex')
    }
}
